      attributes(global) subroutine ax_cuf2(w,u,ur,us,ut,
     &                gxyz,dxm1,dxtm1)

      include 'SIZE_CUF'

      real, intent(out) :: w(lx1,ly1,lz1,lelt)
      real, intent(in)  :: u(lx1,ly1,lz1,lelt)
      real ur  (lx1,ly1,lz1,lelt)
      real us  (lx1,ly1,lz1,lelt)
      real ut  (lx1,ly1,lz1,lelt)

      real gxyz(lx1,ly1,lz1,2*ldim,lelt)

      real, intent(in) :: dxm1(lx1,lx1)
      real, intent(in) :: dxtm1(lx1,lx1)

      real rtmp,stmp,ttmp,wijk1e,wijk2e
      real, shared :: shdxm1(lx1,lx1)
      real, shared :: shdxtm1(lx1,lx1)

      real, shared :: shur(lx1,ly1,lz1)
      real, shared :: shus(lx1,ly1,lz1)
c      real, shared :: shut(lx1,ly1,lz1)
      integer e,i,j,k,l

      e = blockIdx%x
      k = threadIdx%z
      j = threadIdx%y
      i = threadIdx%x

      if (k.eq.1) then
         shdxm1(i,j) = dxm1(i,j)
         shdxtm1(i,j) = dxtm1(i,j)
      end if
      call syncthreads()

      rtmp = 0.0
      stmp = 0.0
      ttmp = 0.0
      do l=1,lx1
        rtmp = rtmp+shdxm1(i,l)*u(l,j,k,e)
        stmp = stmp+shdxm1(j,l)*u(i,l,k,e)
        ttmp = ttmp+shdxm1(k,l)*u(i,j,l,e)
      enddo
          shur(i,j,k) = gxyz(i,j,k,1,e)*rtmp
     $                + gxyz(i,j,k,2,e)*stmp
     $                + gxyz(i,j,k,3,e)*ttmp

          shus(i,j,k) = gxyz(i,j,k,2,e)*rtmp
     $                + gxyz(i,j,k,4,e)*stmp
     $                + gxyz(i,j,k,5,e)*ttmp

          ut(i,j,k,e) = gxyz(i,j,k,3,e)*rtmp
     $                + gxyz(i,j,k,5,e)*stmp
     $                + gxyz(i,j,k,6,e)*ttmp

      rtmp = 0.0
      stmp = 0.0
      ttmp = 0.0
      do l=1,lx1
        rtmp = rtmp+shdxm1(i,l)*u(l,j,k+7,e)
        stmp = stmp+shdxm1(j,l)*u(i,l,k+7,e)
        ttmp = ttmp+shdxm1(k+7,l)*u(i,j,l,e)
      enddo
          shur(i,j,k+7) = gxyz(i,j,k+7,1,e)*rtmp
     $                  + gxyz(i,j,k+7,2,e)*stmp
     $                  + gxyz(i,j,k+7,3,e)*ttmp

          shus(i,j,k+7) = gxyz(i,j,k+7,2,e)*rtmp
     $                  + gxyz(i,j,k+7,4,e)*stmp
     $                  + gxyz(i,j,k+7,5,e)*ttmp

          ut(i,j,k+7,e) = gxyz(i,j,k+7,3,e)*rtmp
     $                  + gxyz(i,j,k+7,5,e)*stmp
     $                  + gxyz(i,j,k+7,6,e)*ttmp


      call syncthreads()

      wijk1e = 0.0
      wijk2e = 0.0
      do l=1,lx1
               wijk1e = wijk1e + shdxtm1(i,l)*shur(l,j,k) 
     $                         + shdxtm1(j,l)*shus(i,l,k)
     $                         + shdxtm1(k,l)*ut(i,j,l,e)
               wijk2e = wijk2e + shdxtm1(i,l)*shur(l,j,k+7) 
     $                         + shdxtm1(j,l)*shus(i,l,k+7)
     $                         + shdxtm1(k+7,l)*ut(i,j,l,e)
      enddo
      w(i,j,k,e) = wijk1e
      w(i,j,k+7,e) = wijk2e

      return
      end
